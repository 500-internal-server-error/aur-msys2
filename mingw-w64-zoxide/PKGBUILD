# Maintainer: Some One <some.one@some.email.com>

_realname=zoxide
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.4
pkgrel=1
pkgdesc="A smarter cd command. Supports all major shells."
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/ajeetdsouza/zoxide'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-fzf: for interactive selection")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/ajeetdsouza/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('ec002bdca37917130ae34e733eb29d4baa03b130c4b11456d630a01a938e0187')

prepare() {
  #CARGO_HOME="/c/Users/User/Applications/cargo"
  #PATH="$CARGO_HOME/bin:$PATH"

  cd "${srcdir}/${_realname}-${pkgver}"

  # if cargo wants to make an http request at build stage, use `cargo --fetch --locked` instead 
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo build --release --frozen --all-features
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" "${srcdir}/${_realname}-${pkgver}/LICENSE"
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/man/man1" "${srcdir}/${_realname}-${pkgver}/man/man1/"*.1
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}" "${srcdir}/${_realname}-${pkgver}/README.md"

  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions" "${srcdir}/${_realname}-${pkgver}/contrib/completions/_zoxide"
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d" "${srcdir}/${_realname}-${pkgver}/contrib/completions/zoxide.fish"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/contrib/completions/zoxide.bash" "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/zoxide"
}
