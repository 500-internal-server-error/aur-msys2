_realname=rustup
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.27.1
pkgrel=1
pkgdesc="The Rust toolchain installer"
arch=('x86_64')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/rust-lang/rustup.rs"
license=('spdx:Apache-2.0 AND MIT')
msys2_references=(
	'archlinux: rustup'
)
depends=(
	"${MINGW_PACKAGE_PREFIX}-curl"
	"${MINGW_PACKAGE_PREFIX}-xz"
	"${MINGW_PACKAGE_PREFIX}-zstd"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
optdepends=(
	"${MINGW_PACKAGE_PREFIX}-gdb: rust-gdb script"
	"${MINGW_PACKAGE_PREFIX}-lldb: rust-lldb script"
)
provides=("${MINGW_PACKAGE_PREFIX}-rust")
conflicts=("${MINGW_PACKAGE_PREFIX}-rust")
install='post.install'
options=("!lto")
source=(
	"rustup-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"rustup-profile.sh"
)
sha256sums=('f5ba37f2ba68efec101198dca1585e6e7dd7640ca9c526441b729a79062d3b77'
            'd9cc7662586925ed22c335ebfe62a11acbe72c7f7d4edaba018e88937dfdc9d6')
_binlinks=('cargo' 'rustc' 'rustdoc' 'rust-gdb' 'rust-lldb' 'rustfmt' 'cargo-fmt' 'cargo-clippy' 'clippy-driver' 'cargo-miri')

build() {
	cd "${_realname}-${pkgver}"
	cargo build --release --features no-self-update --bin rustup-init
}

package() {
	cd "${_realname}-${pkgver}"
	install -d "${pkgdir}/${MINGW_PREFIX}/lib/${_realname}/bin"
	install -Dm755 "target/release/rustup-init" "${pkgdir}/${MINGW_PREFIX}/bin/rustup.exe"
	for link in "${_binlinks[@]}"; do
		MSYS=winsymlinks:native ln "${pkgdir}/${MINGW_PREFIX}/bin/rustup.exe" "${pkgdir}/${MINGW_PREFIX}/bin/${link}.exe"
	done

	# Special treatment to allow for rust-analyzer to still allow the separate package version to be used.
	MSYS=winsymlinks:native ln "${pkgdir}/${MINGW_PREFIX}/bin/rustup.exe" "${pkgdir}/${MINGW_PREFIX}/lib/${_realname}/bin/rust-analyzer.exe"

	install -Dm644 "${srcdir}/rustup-profile.sh" "${pkgdir}/etc/profile.d/rustup.sh"

	# Generate completion files.
	mkdir -p "${pkgdir}/${MINGW_PREFIX}/share/bash-completion/completions"
	"${pkgdir}/${MINGW_PREFIX}/bin/rustup" completions bash > "${pkgdir}/${MINGW_PREFIX}/share/bash-completion/completions/rustup"
	"${pkgdir}/${MINGW_PREFIX}/bin/rustup" completions bash cargo > "${pkgdir}/${MINGW_PREFIX}/share/bash-completion/completions/cargo"
	mkdir -p "${pkgdir}/${MINGW_PREFIX}/share/fish/vendor_completions.d"
	"${pkgdir}/${MINGW_PREFIX}/bin/rustup" completions fish > "${pkgdir}/${MINGW_PREFIX}/share/fish/vendor_completions.d/rustup.fish"
	mkdir -p "${pkgdir}/${MINGW_PREFIX}/share/zsh/site-functions"
	"${pkgdir}/${MINGW_PREFIX}/bin/rustup" completions zsh > "${pkgdir}/${MINGW_PREFIX}/share/zsh/site-functions/_rustup"
	"${pkgdir}/${MINGW_PREFIX}/bin/rustup" completions zsh cargo > "${pkgdir}/${MINGW_PREFIX}/share/zsh/site-functions/_cargo"

	install -Dm644 LICENSE-MIT "${pkgdir}/${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE-MIT"
	install -Dm644 LICENSE-APACHE "${pkgdir}/${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE-APACHE"
}
