pkgname='skim'
pkgver='0.20.5'
pkgrel=2
pkgdesc='Fuzzy Finder in Rust!'
arch=(x86_64)
url='https://github.com/lotabout/skim'
license=('spdx:MIT')
depends=('ncurses')
makedepends=('git' 'rust' 'ncurses-devel')
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'fix-cygwin-build.patch'
)
sha512sums=('ad5fd855b7ccff4314d9da7426fb2f1a6c0519df96bdc49304e454a2c7a1efa40bc55ee7a99fd546622524b710d0d17b5421ac0ed810fadb817999d558e4319a'
            '572e483f2abd0099a204ffe789b911665f7339fb1692befae9c7574ab3ba2c9081ed0c591e31e63c64f0c7b541c43c69dc3acbd9c00c51139ca9d38e98d205ee')
b2sums=('d9c8f4e7445a78c02db7e390d38fcd2789e3439e32a9092c72658ba7a610ea37476bd483944462f398e59a9445c1cbcb1154dc0dd1235a16c5f157a8c221e4c1'
        'a16ab06e554d45bda97406f4af8c50fb9364848a5661dbaf96e61debc4e640a3dc9a9ef9a5e823871e7b3f77f40f770d573a18a605579eb6f84f36b61d350dca')

prepare() {
  cd "$pkgname"

  patch -Np1 -i "${srcdir}/fix-cygwin-build.patch"
  # download dependencies
  cargo fetch --frozen --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
