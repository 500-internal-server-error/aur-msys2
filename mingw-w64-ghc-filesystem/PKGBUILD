_shortname='filesystem'
_realname='ghc-filesystem'
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc='An implementation of C++17 std::filesystem for C++11/C++14/C++17/C++20 (mingw-w64)'
pkgver=1.5.14
pkgrel=2
url='https://github.com/gulrak/filesystem'
license=('spdx:MIT')
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
makedepends=(
	"${MINGW_PACKAGE_PREFIX}-cc"
	"${MINGW_PACKAGE_PREFIX}-cmake"
	"${MINGW_PACKAGE_PREFIX}-ninja"
)
depends=()
source=("https://github.com/gulrak/filesystem/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('e783f672e49de7c5a237a0cea905ed51012da55c04fbacab397161976efc8472')

build() {
	declare -a extra_config
	if check_option 'debug' 'n'; then
		extra_config+=('-DCMAKE_BUILD_TYPE=Release')
	else
		extra_config+=('-DCMAKE_BUILD_TYPE=Debug')
	fi

	# flags and tests disabled according to:
	# https://github.com/gulrak/filesystem/issues/189#issuecomment-2571334165
	MSYS2_ARG_CONV_EXCL='-DCMAKE_INSTALL_PREFIX=' \
	"${MINGW_PREFIX}/bin/cmake" \
		-G Ninja \
		"${extra_config[@]}" \
		-DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
		-DCMAKE_CXX_FLAGS='-Wa,-mbig-obj' \
		-DGHC_FILESYSTEM_BUILD_STD_TESTING=NO \
		-S "${_shortname}-${pkgver}" \
		-B "build-${MSYSTEM}"

	"${MINGW_PREFIX}/bin/cmake" --build "build-${MSYSTEM}"
}

check() {
	"${MINGW_PREFIX}/bin/cmake" --build "build-${MSYSTEM}" --target test
}

package() {
	DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "build-${MSYSTEM}"

	install -Dm644 "${srcdir}/${_shortname}-${pkgver}/README.md" -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
	install -Dm644 "${srcdir}/${_shortname}-${pkgver}/LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
